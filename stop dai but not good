#define button_next 2 
#define button_play 3
#define button_before 4
const int SpeakerPin = 11;

// เพลง
int NooMaLee[]   = {330,294,262,294,330,330,330,294,294,294,330,392,392,330,294,262,294,330,330,330,294,294,330,294,262};
int HBD[]        = {262,262,294,262,339,330,262,262,294,262,339,330,262,262,523,440,349,330,294,262,494,494,440,349,392,349};
int LittleStar[] = {392,392,587,587,659,659,587,523,523,494,494,440,440,392,
                    587,587,523,523,494,494,440,587,587,523,523,494,494,440,
                    392,392,587,587,659,659,587,523,523,494,494,440,440,392};
const int numNooMaLee = 25;
const int numHBD = 26;
const int numLittleStar = 42;

// FSM
enum {defa,next,before,play,stop};
unsigned long cstate = defa;
int current_song = 0;

// debounce
int buttonState_play = HIGH;
int lastButtonState_play = HIGH;
unsigned long lastDebounceTime = 0;
unsigned long debounceDelay = 50;

void setup() {
  pinMode(SpeakerPin, OUTPUT);
  pinMode(button_next , INPUT_PULLUP);
  pinMode(button_before , INPUT_PULLUP);
  pinMode(button_play , INPUT_PULLUP);
  Serial.begin(9600);
}

void loop() {
  // --- ปุ่ม next / before ---
  if(digitalRead(button_next) == LOW){
    delay(200);
    current_song = (current_song+1)%3;
    Serial.print("Next song: "); Serial.println(current_song);
  }
  if(digitalRead(button_before) == LOW){
    delay(200);
    current_song = (current_song+2)%3;
    Serial.print("Prev song: "); Serial.println(current_song);
  }

  // --- ปุ่มกลาง toggle play/stop ---
  int reading = digitalRead(button_play);
  if (reading != lastButtonState_play) {
    lastDebounceTime = millis();
  }
  if ((millis() - lastDebounceTime) > debounceDelay) {
    if (reading != buttonState_play) {
      buttonState_play = reading;
      if(buttonState_play == LOW){   // กดลง
        if(cstate == play){
          cstate = stop;   // toggle เป็น stop
          Serial.println("Stopped");
        }else{
          cstate = play;   // toggle เป็น play
          Serial.println("Play");
        }
      }
    }
  }
  lastButtonState_play = reading;

  // --- state machine ---
  if(cstate == play){
    Serial.println("Playing...");
    if(current_song == 0) playSong(NooMaLee,numNooMaLee);
    else if(current_song == 1) playSong(HBD,numHBD);
    else if(current_song == 2) playSong(LittleStar,numLittleStar);
  }
  else if(cstate == stop){
    noTone(SpeakerPin);
  }
}

void playSong(int notes[], int len){
  for(int i=0;i<len;i++){
    // อ่านปุ่มระหว่างเล่น
    if(digitalRead(button_play) == LOW){
      delay(50); // debounce เล็กน้อย
      if(digitalRead(button_play) == LOW){
        cstate = stop;
        Serial.println("Force stop while playing");
        break;
      }
    }

    if(cstate == stop) break; // ป้องกันซ้ำ

    tone(SpeakerPin, notes[i]);
    delay(200);
    noTone(SpeakerPin);
    delay(100);
  }
}
